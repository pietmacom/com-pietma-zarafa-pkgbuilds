# Maintainer: MartiMcFly martimcfly@autorisation.de

_pkgname=('mdm')
pkgname=("zarafa-webaccess-${_pkgname}")
groups=('zarafa')
pkgver=2.1
pkgrel=5
pkgdesc='Mobile Device Management plugin for Zarafa Webaccess'
arch=('any')
url='https://community.zarafa.com/mod/community_plugins/download.php?release_guid=9498'
source=('mdm2.zip::https://community.zarafa.com/mod/community_plugins/download.php?release_guid=9498'
        'zarafa-webaccess-mdm.ini'
        'mdm_login.patch'
        'compress-static')
backup=("etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php")
license=('AGPL3')
depends=('zarafa-webaccess'
	 'z-push'
	 'php<7'
	 'php-fpm<7')
md5sums=('1de30b0292198e25f07c4ea9159901bf'
         'de837fc5ad2e8f2d70df5037f2a0a107'
         '88b10c74bc6e7194c52e282a336e1ac7'
         'd737d82dfab24adc516c001238a4119f')

package() {
    cd ${srcdir}/${_pkgname}

    patch -BNp1 -i ${srcdir}/mdm_login.patch

    # plugin
    mkdir -p ${pkgdir}/usr/share/webapps/zarafa-webaccess/plugins/${_pkgname}/
    cp -R * ${pkgdir}/usr/share/webapps/zarafa-webaccess/plugins/${_pkgname}/
    rm -f ${pkgdir}/usr/share/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php

    ## precompress for nginx
    ${srcdir}/compress-static ${pkgdir}/usr/share/webapps/zarafa-webaccess/plugins/${_pkgname}/

    # config
    mkdir -p ${pkgdir}/etc/webapps/zarafa-webaccess/plugins/${_pkgname}/

    ## config mains
    cp config.php ${pkgdir}/etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php
    ln -s /etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php ${pkgdir}/usr/share/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php

    ## perform settings
    ## => replace enable z-push2
    sed -i -e "s/\(zpush-2'] = \)\(.*\)\(;$\)/\1true\3/" ${pkgdir}/etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php
    ## => replace with https://
    sed -i -e "s/\(zpush-url'] = \)\(.*\)\(;$\)/\1\"https\:\/\/localhost\"\3/" ${pkgdir}/etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php

    ## config examples      
    cp ${pkgdir}/etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.php ${pkgdir}/etc/webapps/zarafa-webaccess/plugins/${_pkgname}/config.example.php

    # php
    mkdir -p ${pkgdir}/etc/php/conf.d
    cp ${srcdir}/${pkgname}.ini ${pkgdir}/etc/php/conf.d
}
